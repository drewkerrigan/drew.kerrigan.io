---
layout: post
title: Distributed Elixir
permalink: "ditributed-elixir"
summary: Chat Server and Client Example
author: 
    name: "Drew Kerrigan"
---

<h3>&ldquo;Elixir looks cool, what does distributed elixir look like?&rdquo;</h3>

<p>One common question that I&rsquo;ve received when talking to people about <a href="http://elixir-lang.org/">Elixir</a> is <code>"what does distributed elixir look like?"</code> This post uses code I wrote for <a href="http://erlangdc.org/">Erlangdc</a> R13B this year. It&rsquo;s essentially the same content as the slides for my talk ( <a href="http://kerrigan.io/erlangdc/#/">kerrigan.io/erlangdc</a> ) if you prefer that format.</p>

<h3>Getting Started</h3>

<h4>Create an OTP application</h4>

<p>When you create a default application with mix (as of 0.11.*) it will create an OTP application along with a supervisor skeleton for you.</p>

<code>mix new ex_messenger</code>


<p>Application: <code>lib/ex_messenger.ex</code></p>

<p>Supervisor: <code>lib/ex_messenger/supervisor.ex</code></p>

<p>Here&rsquo;s what <code>lib/ex_messenger/supervisor.ex</code> looks like without modifications:</p>

<pre><code>def init([]) do
children = [
    # Define workers and child supervisors to be supervised
    # worker(ExMessenger.Worker, [])
]

# See http://elixir-lang.org/docs/stable/   Supervisor.Behaviour.html
# for other strategies and supported options
supervise(children, strategy: :one_for_one)
end
</code></pre>


<p>As you can see, the application has a supervisor, but doesn&rsquo;t have anything to supervise yet, lets go ahead and add a reference to a worker we&rsquo;ll call <code>ExMessenger.Server</code></p>

<pre><code>def init([]) do
  # Define workers and child supervisors to be supervised
  children = [ worker(ExMessenger.Server, [[]]) ]
  supervise children, strategy: :one_for_one
end</code></pre>


<h4>Create a (gen) server</h4>

<p>Create a new file at lib/ex_messenger/server.ex</p>

<h5>Respond to calls: connect and disconnect</h5>

<pre><code>defmodule ExMessenger.Server do
  use GenServer.Behaviour

  def start_link([]) do
      :gen_server.start_link({ :local, :message_server }, __MODULE__, [], [])
  end

  def init([]) do
      { :ok, HashDict.new() }
  end

  def handle_call({:connect, nick}, {pid, _}, users) do
      newusers = users |&gt; HashDict.put(nick, node(pid))
      userlist = newusers |&gt; HashDict.keys |&gt; Enum.join ":"
      {:reply, {:ok, userlist}, newusers}
  end

  def handle_call({:disconnect, nick}, {pid, _}, users) do
      newusers = users |&gt; HashDict.delete nick
      {:reply, :ok, newusers}
  end

  def handle_call(_, _, users), do: {:reply, :error, users}

  def handle_cast(_, users), do: {:noreply, users}
end</code></pre>


<p>I won&rsquo;t go into the details of what a gen_server or behaviour is because it&rsquo;s out of the scope for this post, but feel free to dig into Erlang OTP and gen_servers here: <a href="http://www.erlang.org/doc/design_principles/gen_server_concepts.html">erlang.org/doc/design_principles/gen_server_concepts.html</a></p>

<p>In the code above, there are 3 things to note:</p>

<ol>
<li><code>{:ok, HashDict.new()}</code>: This is the initial state of our chat server. We need our server to keep track of all of the connected users, and we can pretty easily do so with a <code>HashDict</code>.</li>
<li><code>newusers = users |&gt; HashDict.put(nick, node(pid))</code>: When we receive a call from a client to connect to the chat server, we&rsquo;ll add them to the <code>HashDict</code> of users.</li>
<li><code>newusers = users |&gt; HashDict.delete nick</code>: As you might expect, when we get a <code>disconnect</code> call, that client is removed from the list.</li>
</ol>


<p>One more thing: The key for the HashDict records is the client&rsquo;s nickname, and the values are the erlang node names. An example nodename might be <code>"client@192.168.1.10"</code></p>

<h5>Broadcast a message to all clients</h5>

<p>Here is what a broadcast function might look like</p>

<pre><code>defp broadcast(users, from, msg) do
  Enum.each(users, fn { _, node } -&gt; :gen_server.cast({:message_handler, node}, {:message, from, msg}) end)
end</code></pre>


<p>Here we are applying an operation to each member of the users HashDict. We&rsquo;re sending a gen_server call to a registered server called <code>:message_handler</code> that doesn&rsquo;t exist yet, but it will once we create the client.</p>

<h5>Respond to calls: say and private_message</h5>

<pre><code>def handle_cast({:say, nick, msg}, users) do
  ears = HashDict.delete(users, nick)
  broadcast(ears, nick, "#{msg}")

  {:noreply, users}
end

def handle_cast({:private_message, nick, receiver, msg}, users) do
  case users |&gt; HashDict.get receiver do
      nil -&gt; :ok
      r -&gt;
          :gen_server.cast({:message_handler, r}, {:message, nick, "(#{msg})"})
  end
  {:noreply, users}
end</code></pre>


<p>We are defining the <code>:say</code> cast to just be a broadcast of <code>msg</code> from <code>nick</code> to all connected users.</p>

<p>The <code>:private_message</code> cast is the important bit from the broadcast function above, but with only a single target.</p>

<p>The reason for choosing <code>handle_cast</code> over <code>handle_call</code> for these is that they do not modify the state of the server, and therefore shouldn&rsquo;t require responses to the client that sent them.</p>

<h4>Chat Server Code</h4>

<p>The code thus far can be found at <a href="https://github.com/drewkerrigan/ex_messenger">github.com/drewkerrigan/ex_messenger</a>. Please check it out and try to run it, the above code is simplified and missing a few important validation pieces like making sure two clients with the same name cannot connect.</p>

<h4>Running the Server</h4>

<p>For local testing:</p>

<pre><code>iex --sname server --cookie chocolate-chip -S mix</code></pre>


<p>For external testing (find your LAN ip with <code>ifconfig</code>):</p>

<pre><code>iex --name server@ --cookie chocolate-chip -S mix</code></pre>


<p>Full Code: <a href="https://github.com/drewkerrigan/ex_messenger">ex_messenger</a></p>

<h3>(CLI) Client</h3>

<p>Create a new mix project with the &mdash;bare flag</p>

<pre><code>mix new ex_messenger_client --bare</code></pre>


<p>This generates a simple application with lib/ex_messenger_client.ex</p>

<pre><code>defmodule ExMessengerClient do
end</code></pre>


<h4>Process Server Calls</h4>

<p>We will get to the CLI application soon, but first lets respond to calls from the server</p>

<p>lib/ex_messenger_client.ex</p>

<pre><code>defmodule ExMessengerClient do
end

defmodule ExMessengerClient.MessageHandler do
  use GenServer.Behaviour

  def start_link(server) do
      :gen_server.start_link({ :local, :message_handler }, __MODULE__, server, [])
  end

  def init(server) do
      { :ok, server }
  end

  def handle_call(_, _, server), do: {:reply, :error, server}

  def handle_cast({:message, nick, msg}, server) do
      msg = String.rstrip(msg)
      IO.puts "\n#{server}&gt; #{nick}: #{msg}"
      IO.write "#{Node.self()}&gt; "
      {:noreply, server}
  end

  def handle_cast(_, server), do: {:noreply, server}
end</code></pre>


<h4>CLI Application</h4>

<pre><code>defmodule ExMessengerClient do

  def main(args) do
      args |&gt; parse_args |&gt; process
  end

  def parse_args(args) do
      switches =
          [
          help: :boolean,
          server: :string,
          nick: :string
          ]

      aliases =
          [
          h: :help,
          s: :server,
          n: :nick
          ]

      options = OptionParser.parse(args, switches: switches, aliases: aliases)

      case options do
          { [ help: true], _, _}            -&gt; :help
          { [ server: server], _, _}        -&gt; [server]
          { [ server: server, nick: nick], _, _} -&gt; [server, nick]
          _                                 -&gt; []
      end
  end

  def process(:help) do
      IO.puts """
          Usage:
          ./ex_messenger_client -s server_name [-n nickname]

          Options:
          -s, --server = fully qualified server name
          -n, --nick   = nickname (optional, you will be promted if not specified)

          Example:
          ./ex_messenger_client -s server@192.168.1.1 -n dr00

          Options:
          -h, [--help]      # Show this help message and quit.
      """
      System.halt(0)
  end

  def process([]) do
      process([nil, nil])
  end

  def process([server]) do
      process([server, nil])
  end

  def process([server, nick]) do
      server = case server do
          nil -&gt;
          IO.write "Server Name: "
          IO.read :line
          n -&gt; n
      end

      server = list_to_atom(bitstring_to_list(String.rstrip(server)))

      IO.puts "Connecting to #{server} from #{Node.self()}..."
      Node.set_cookie(Node.self(), :"chocolate-chip")
      case Node.connect(server) do
          true -&gt; :ok
          reason -&gt;
              IO.puts "Could not connect to server, reason: #{reason}"
              System.halt(0)
      end

      ExMessengerClient.MessageHandler.start_link(server)

      IO.puts "Connected"

      nick = case nick do
          nil -&gt;
          IO.write "Nickname: "
          IO.read :line
          n -&gt; n
      end

      nick = String.rstrip(nick)

      case :gen_server.call({:message_server, server}, {:connect, nick}) do
          {:ok, users} -&gt;
              IO.puts "**Joined the chatroom**"
              IO.puts "**Users in room: #{users}**"
              IO.puts "**Type /help for options**"
          reason -&gt;
              IO.puts "Could not join chatroom, reason: #{reason}"
              System.halt(0)
      end

      # Start gen_server to handle input / output from server
      input_loop([server, nick])
  end

  def input_loop([server, nick]) do
      IO.write "#{Node.self()}&gt; "
      command = IO.read :line
      handle_command(command, [server, nick])

      input_loop([server, nick])
  end

  def handle_command(command, [server, nick]) do
      command = String.rstrip(command)
      case command do
          "/help" -&gt;
              IO.puts """
                  Avaliable commands:
                  /leave
                  /join
                  /pm  
                  or just type a message to send
              """
          "/leave" -&gt;
              :gen_server.call({:message_server, server}, {:disconnect, nick})
              IO.puts "You have exited the chatroom, you can rejoin with /join or quit with -c a"
          "/join" -&gt;
              IO.inspect :gen_server.call({:message_server, server}, {:connect, nick})
              IO.puts "Joined the chatroom"
          "" -&gt;
              :ok
          nil -&gt;
              :ok
          message -&gt;
              if String.contains? message, "/pm" do
                  [to|message] = String.split(String.slice(message, 4..-1))
                  message = String.lstrip(List.foldl(message, "", fn(x, acc) -&gt; "#{acc} #{x}" end))
                  :gen_server.cast({:message_server, server}, {:private_message, nick, to, message})
              else
                  :gen_server.cast({:message_server, server}, {:say, nick, message})
          end
      end
  end
end</code></pre>


<h4>Mix.exs changes</h4>

<p>We also need to make a change to mix.exs for the CLI application to work properly</p>

<p>mix.exs</p>

<pre><code>def project do
  node = System.get_env("node")
  node = case node do
      nil -&gt; "client"
      n -&gt; n
  end

  mode = System.get_env("mode")
  mode = case mode do
      nil -&gt; "-sname"
      "external" -&gt; "-name"
  end

  [ app: :ex_messenger_client,
  version: "0.0.1",
  deps: deps,
  escript_emu_args: "%%!#{mode} #{node}\n"]
end
...</code></pre>


<h3>Makefile</h3>

<p>In order to compile the escript for multiple clients quickly, I also made a Makefile to make things faster</p>

<p>Makefile</p>

<pre><code>.PHONY: all

all:
mix

run:
rm -f ex_messenger_client
mix escriptize
./ex_messenger_client</code></pre>


<h4>Running the Client</h4>

<p>For local testing:</p>

<pre><code>node=client make run</code></pre>


<p>For external testing (find your LAN ip with <code>ifconfig</code>):</p>

<pre><code>mode=external node=client@ make run</code></pre>


<p>Note: Mode and Node are 2 different env variables</p>

<p>Full Code: <a href="https://github.com/drewkerrigan/ex_messenger_client">ex_messenger_client</a></p>

<h2>Thanks for Reading!</h2>

<h3>Resources</h3>

<ul>
<li><p>Chat Server: <a href="https://github.com/drewkerrigan/ex_messenger">ex_messenger</a></p></li>
<li><p>Client: <a href="https://github.com/drewkerrigan/ex_messenger_client">ex_messenger_client</a></p></li>
</ul>
